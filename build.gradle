plugins {
    id 'war'
    id "com.diffplug.spotless" version "7.2.1"
//    id "com.github.spotbugs" version "6.0.18"
}

description = 'Down time manager app'
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.smoothnessVersion = '4.12.2'
ext.releaseDate = new Date().format('MMM dd yyyy')

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation "org.jlab:smoothness-weblib:${smoothnessVersion}"
    //'org.apache.httpcomponents:httpclient:4.5.14',
    //'org.apache.httpcomponents:httpmime:4.5.14'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4',
            'org.jlab:jlog:5.1.0',
            'org.apache.poi:poi:5.2.3',
            'org.apache.poi:poi-ooxml:5.2.3'

    // Wildfly 26.1.3.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
            'javax.servlet:jstl:1.2',
            'org.hibernate:hibernate-core:5.3.28.Final',
            'org.hibernate:hibernate-envers:5.3.28.Final',
            'org.jsoup:jsoup:1.14.2'
}
tasks.named('jar') {
    enabled = false
}
war {
    archiveFileName = 'dtm.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version.toString())
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@SMOOTHNESS_VERSION@", smoothnessVersion)
        }
    }
}
spotless {
    java {
        googleJavaFormat()
    }
}