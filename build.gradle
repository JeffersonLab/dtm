plugins {
    id 'war'
    id "com.diffplug.spotless" version "7.2.1"
//    id "com.github.spotbugs" version "6.0.18"
}

description = 'Down time manager app'
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.smoothnessVersion = '5.0.2'
ext.releaseDate = new Date().format('MMM dd yyyy')

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation "org.jlab:smoothness-weblib:${smoothnessVersion}"

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:5.1.3',
            'org.jlab:jlog:5.2.0',
            'org.apache.poi:poi:5.4.1',
            'org.apache.poi:poi-ooxml:5.4.1'

    // Wildfly 37.0.1.Final provides:
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:10.0.0',
            'org.hibernate:hibernate-core:6.6.19.Final',
            'org.hibernate:hibernate-envers:6.6.19.Final',
            'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1',
            'com.oracle.database.jdbc:ojdbc17:23.9.0.25.07'

    // Latest Wildfly no longer includes...
    implementation 'org.jsoup:jsoup:1.21.2'
}
tasks.named('jar') {
    enabled = false
}
war {
    archiveFileName = 'dtm.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version.toString())
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@SMOOTHNESS_VERSION@", smoothnessVersion)
        }
    }
}
spotless {
    java {
        googleJavaFormat()
    }
}